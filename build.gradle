/**
 * Gradle 빌드 전 실행되는 설정
 */
buildscript {
	ext {
		springBootVersion = '2.5.4'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
	}
}

subprojects {
	/**
	 * Gradle 7.0 이상부터는 api 키워드를 사용할 수 있다.
	 * api 키워드를 사용하기 위해서 'java' 플러그인 대신, 'java-library' 플러그인을 적용해야 한다.
	 */
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.zoopi'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = '11'

	repositories {
		mavenCentral()
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	/**
	 * 각 모듈에 공통으로 사용할 의존성
	 */
	dependencies {
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

/**
 * 각각 module-api, module-service 모듈에 module-common 의존성을 추가
 */
project(':module-api') {
	dependencies {
		implementation project(':module-common')
		implementation project(':module-domain')
	}
}

project(':module-domain') {
	dependencies {
		implementation project(':module-common')
	}
}