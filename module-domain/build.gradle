plugins {
    id "com.ewerk.gradle.plugins.querydsl" version "1.0.10" // 외부 플러그인 적용
}

/**
 * bootJar 는 실행 가능한 jar을 만들기에, main 함수 필요
 * main 함수가 없는 모듈이므로 enabled = false 설정 추가
 */
bootJar {
    enabled = false
}

jar {
    enabled = true
}

/**
 * implementation 은 직접 의존하는 모듈에서만 참조가능하다.
 * compile(Gradle 7.0 미만) 혹은 api(Gradle 7.0 이상) 은 현재 모듈을 의존하는 다른 모듈들도 참조가능하다.
 */
dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.querydsl:querydsl-jpa'

    testImplementation 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
}

// QClass 생성 위치
def querydslDir = "$buildDir/generated/querydsl"

querydsl {
    library = 'com.querydsl:querydsl-apt'
    jpa = true
    querydslSourcesDir = querydslDir
}

// QClass 소스 위치 지정
sourceSets {
    main.java.srcDir querydslDir
}

// gradle 5.0 설정
configurations {
    querydsl.extendsFrom compileClasspath
}

compileQuerydsl {
    options.annotationProcessorPath = configurations.querydsl
}